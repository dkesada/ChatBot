Pregunta -> Peticion Caracs | Caracs
Peticion -> Quiero | Ensena | Que | Cual | Gustaria | Hay

Quiero -> "quiero" VerbInf Sn | "quiero" Sn | "quiero" "algun" Sn | "quiero" "que" "me" VerbPet Sn

Ensena -> EnsenaT Sn
EnsenaT -> "ensename" | "muestrame" | "mirame"

Que -> "que" | "que" Sn | "que" Sn VerbP | "que" TipoP VerbP Disp

Gustaria -> "me" "gustaria" VerbInf Sn

Hay -> "hay" | "hay" Sn 

VerbPet -> "ensenes" | "muestres"
VerbInf -> "ver" | "mirar" | "encontrar" | Op
Op -> "alquilar" | "comprar"

Sn -> Det TipoS | TipoP | Det TipoS Disp | TipoP Disp
Det -> "un" | "una" | "algun" | "alguna"
TipoS -> "casa" |  "piso" | "duplex" | "chalet" | "atico" | "casa" "rustica" | "casa" "rural"
TipoP -> "casas" | "pisos" | "duplex" | "chalets" | "aticos" | "casas" "rusticas" | "casas" "rurales"
Disp -> "para" VerbInf | "a" "la" "venta"

Caracs -> Carac Caracs | Carac Conj Caracs | Carac
Carac -> Precio | Lugar | Tamano | Estado | Muebles | Habit | Banos | Alq | TipoP | TipoS | Accion | Disp
Conj -> "y"

Precio -> Cuantif Cant | Cuantif Cant Mes
Cuantif -> Prep | Prep Limit 
Prep -> "a" | "por" | "de" | "que" VerbP | "con" | "en" | "por" | "alrededor" "de" |"del" | "al" | "sin"
VerbP -> "valga" | "valgan" | "sea" "de" | "sean" "de" |  "este" | "esten" | "sea" | "sean" | "necesite" | "necesiten" | "tenga"  | "tengan" | "venga" | "vengan" | "hay"
Limit -> "menos" "de" | "como" "mucho" | "alrededor" "de" | "menos" | "mas" | "mas" "de" | "al" "menos"
Cant -> Nums "EUR" | Nums "euros" | Nums 
Nums -> "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" | "num" | NumsOrd
NumsOrd -> "un" | "una" | "dos" | "tres" | "cuatro" | "cinco" | "seis" | "siete" | "ocho" | "nueve" | "diez"
Mes -> "al" "mes" | "mes"

Lugar -> Prep Zona Nombre | Prep Nombre
Zona -> "la" "zona" "de" | "area" "de"
Nombre -> Torrejon | Mejorada | Coslada | Alcala
Torrejon -> "torrejon" | "torrejon" "de" "ardoz"
Mejorada -> "mejorada" | "mejorada" "del" "campo"
Alcala -> "alcala" | "alcala" "de" "henares"
Coslada -> "coslada"

Tamano -> Cuantif Nums Metros | Cuantif Nums Metros Util
Metros -> "m2" | "metros" | "metros" "cuadrados" 
Util -> "utiles" | "construidos"

Estado -> "que" VerbP Est | Est | Prep Est | "que" VerbP Prep Est
Est ->  Nuevo  | Segunda | Reforma
Nuevo ->  "nuevo" | "nueva" | "nuevos" | "nuevas" | "nueva" "obra"
Segunda -> "segunda" "mano" | "buen" "estado" | "seminuevo" | "seminueva" | "seminuevos" | "seminuevas"
Reforma -> "reformar" | "reformas" | "reformarse"

Muebles -> "que" VerbP Mueb | Mueb | Prep Mueb
Mueb -> MuebTer | "sin" MuebTer | Cocina 
MuebTer -> "amueblado" | "amueblada" | "amueblados" | "amuebladas" | "amueblar"
Cocina -> "cocina" MuebTer | "cocina" MuebTer | "la" "cocina" MuebTer | "la" "cocina" "sin" MuebTer

Habit -> Cuantif Nums Hab | Nums Hab | "sin" Hab 
Hab -> "habitacion" | "habitaciones"

Banos -> Cuantif Nums Ban | Nums Ban | "sin" Ban
Ban -> "bano" | "banos"

Alq -> Prep "alquiler"

Accion -> "para" Op | "puedo" Op
